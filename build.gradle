plugins {
    id 'java'
    id 'checkstyle'
    id 'pmd'
    id 'jacoco'
}

group 'org.arjun.project'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0'
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacocoTestReport{
    dependsOn test
    reports {
        xml.enabled true
        html.enabled true
    }
    finalizedBy jacocoTestCoverageVerification
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            enabled = true
            limit {
                minimum = 0.5
            }
        }
    }
}

configurations {
    checkstyleConfig
}

dependencies {
    checkstyleConfig("com.puppycrawl.tools:checkstyle:8.29") { transitive = false }
}

checkstyle {
    toolVersion '8.29'
    config = resources.text.fromArchiveEntry(configurations.checkstyleConfig, 'google_checks.xml')
    ignoreFailures = true
}

//checkstyle {
//    configFile = file("${rootDir}/config/checkstyle/checkstyle.xml")
//}

tasks.withType(Pmd){
    reports{
        xml.enabled=true
        html.enabled=true
    }
}

pmd {
    ruleSetConfig = rootProject.resources.text.fromFile('config/pmd/pmd-rules.xml')
    pmdTest.enabled=false
    ignoreFailures = true
}
